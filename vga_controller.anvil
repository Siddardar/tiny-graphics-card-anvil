struct vga_signals {
    hsync:  logic,
    vsync:  logic,
    active: logic,
    x:      logic[10],
    y:      logic[10]
}

chan vga_ch {
    left req: (logic @res),
    right res: (vga_signals @#1)
}

proc vga_controller(ep: left vga_ch) {
    reg h_count: logic[10];
    reg v_count: logic[10];
    reg out_buf: vga_signals;

    loop {
        let _ = recv ep.req >>

        // read in scope to release lock
        {
            let hc = *h_count >>
            let vc = *v_count >>
            
            let h_active = (hc < 10'd640) >>
            let v_active = (vc < 10'd480) >>
            let is_active = h_active & v_active >>

            let h_sync_active = (hc >= 10'd656) & (hc < 10'd752) >>
            let hsync = ~h_sync_active >>

            let v_sync_active = (vc >= 10'd490) & (vc < 10'd492) >>
            let vsync = ~v_sync_active >>

            let x_out = if (h_active) { hc } else { 10'd0 } >>
            let y_out = if (v_active) { vc } else { 10'd0 } >>

            set out_buf := vga_signals::{
                hsync  = hsync;
                vsync  = vsync;
                active = is_active;
                x      = x_out;
                y      = y_out
            }
        } >> 

        if (*h_count == 10'd799) {
            set h_count := 10'd0 >>
            
            if (*v_count == 10'd524) {
                set v_count := 10'd0
            } else {
                set v_count := *v_count + 10'd1
            }
        } else {
            set h_count := *h_count + 10'd1
        } >>

        send ep.res (*out_buf) >>
        cycle 1
    }
}